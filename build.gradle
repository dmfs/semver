plugins {
    id 'java-library'
    id 'org.dmfs.gver' version '0.18.0'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0' apply false
}

sourceCompatibility = 1.8

gver {
    issueTracker GitHub {
        repo = "dmfs/semver"
        if (project.hasProperty("GITHUB_API_TOKEN")) {
            accessToken = GITHUB_API_TOKEN
        }
    }
    changes {
        are none when {
            affects only(matches(~/.*\.md/))
        }
        are major when {
            commitMessage contains(~/(?i)#(major|break(ing)?)\b/)
        }
        are minor when {
            commitMessage contains(~/(?i)#(?<issue>\d+)\b/) {
                where("issue") { isIssue { labeled "enhancement" } }
            }
        }
        are patch when {
            commitMessage contains(~/(?i)#(?<issue>\d+)\b/) {
                where("issue") { isIssue { labeled "bug" } }
            }
        }
        are minor when {
            commitMessage contains("#feature\\b")
        }
        otherwise patch
    }
    preReleases {
        on ~/main/ use { "beta" }
        on ~/(.*\/)?(?<name>.*)/ use { "alpha-${group('name')}.1" }
    }
    releaseBranchPattern ~/main$/
}

allprojects {
    group 'org.dmfs'
    repositories {
        mavenCentral()
    }
}

configurations {
    pom
}

if (project.hasProperty('SONATYPE_USERNAME') && project.hasProperty('SONATYPE_PASSWORD')) {
    apply plugin: 'io.github.gradle-nexus.publish-plugin'

    nexusPublishing {
        repositories {
            sonatype {
                username = SONATYPE_USERNAME
                password = SONATYPE_PASSWORD
            }
        }
    }
}


apply from: 'publish.gradle'
apply from: 'jacoco.gradle'

dependencies {
    compileOnly 'org.eclipse.jdt:org.eclipse.jdt.annotation:2.2.600'
    compileOnly 'org.dmfs:srcless-annotations:0.3.0'
    annotationProcessor 'org.dmfs:srcless-processors:0.3.0'
    annotationProcessor 'org.dmfs:nullless-processors:0.3.0'

    implementation "org.dmfs:jems2:2.18.0"

    testImplementation project(":semver-confidence")
    testImplementation 'org.saynotobugs:confidence-core:0.14.0'
    testImplementation 'org.saynotobugs:confidence-incubator:0.14.0'
    testImplementation "org.dmfs:jems2-testing:2.18.0"
    testImplementation "org.dmfs:jems2-confidence:2.18.0"
    testImplementation "org.hamcrest:hamcrest:2.2"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}