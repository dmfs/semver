apply plugin: 'java-library'
apply from: 'publish.gradle'
apply from: 'jacoco.gradle'

def gitVersion = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--always', '--dirty'
        standardOutput = stdout
    }
    def version = stdout.toString().trim()
    // TODO: a SNAPSHOT should have the minor number increased by one (and the patch number reset to 0) https://github.com/dmfs/jems/issues/295
    // TODO: the git version format is not fully compliant to semantic versioning 2.0.0 https://github.com/dmfs/jems/issues/295
    return version.contains('-') ? version + '-SNAPSHOT' : version
}

allprojects {
    group 'org.dmfs'
    version gitVersion()
    repositories {
        mavenCentral()
    }
}


dependencies {
    implementation 'org.dmfs:srcless-annotations:0.0.3'
    annotationProcessor 'org.dmfs:srcless-processors:0.0.3'
    implementation "org.dmfs:jems2:2.8.0"
    testImplementation "org.dmfs:jems2-testing:2.8.0"
    testImplementation "org.hamcrest:hamcrest:2.2"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}