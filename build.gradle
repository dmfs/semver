plugins {
    id 'java-library'
    id 'org.dmfs.gver' version '0.35.0'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0' apply false
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

gver {
    issueTracker GitHub {
        repo = "dmfs/semver"
        if (project.hasProperty("GITHUB_API_TOKEN")) {
            accessToken = GITHUB_API_TOKEN
        }
    }
    changes {
        are none when {
            affects only(matches(~/.*\.md/))
        }
        are major when {
            commitMessage contains(~/(?i)#(major|break(ing)?)\b/)
        }
        are minor when {
            commitMessage contains(~/(?i)#(?<issue>\d+)\b/) {
                where("issue") { isIssue { labeled "enhancement" } }
            }
        }
        are patch when {
            commitMessage contains(~/(?i)#(?<issue>\d+)\b/) {
                where("issue") { isIssue { labeled "bug" } }
            }
        }
        are minor when {
            commitMessage contains(~/#feature\b/)
        }
        are patch otherwise
    }
    preReleases {
        on ~/main/ use { "beta" }
        on ~/(.*\/)?(?<name>.*)/ use { "alpha-${group('name')}.1" }
    }
    releaseBranchPattern ~/main$/
}

allprojects {
    group 'org.dmfs'
    repositories {
        mavenCentral()
    }
}

configurations {
    pom
}

if (project.hasProperty('SONATYPE_USERNAME') && project.hasProperty('SONATYPE_PASSWORD')) {
    apply plugin: 'io.github.gradle-nexus.publish-plugin'

    nexusPublishing {
        repositories {
            sonatype {
                username = SONATYPE_USERNAME
                password = SONATYPE_PASSWORD
            }
        }
    }
}


apply from: 'publish.gradle'
apply from: 'jacoco.gradle'

dependencies {
    compileOnly libs.eclipse.jdt.anntation
    compileOnly libs.srcless.annotations
    annotationProcessor libs.srcless.processors

    api libs.jems2

    testImplementation project(":semver-confidence")
    testImplementation libs.confidence.core
    testImplementation libs.confidence.incubator
    testImplementation libs.jems2.testing
    testImplementation libs.jems2.confidence
    testImplementation libs.junit.jupiter.api
    testRuntimeOnly libs.junit.jupiter.engine
}

test {
    useJUnitPlatform()
}